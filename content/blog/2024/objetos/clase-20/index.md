---
title: Herencia
date: '2024-09-23'
description: Vig√©sima clase de PdeP
tags: [objetos, herencia, super, clases abstractas, redefinici√≥n, diagrama de clases, interfaces]
---

## Tarea para la clase que viene:

- Comenzar la [segunda entrega del TP de objetos](https://docs.google.com/document/d/1tBWSjC5v5_8-s3tKPU3T7slNciWSqFH7k0IVtKz8BLU/edit#heading=h.s5dp5vwhsyw). Realizar los tests correspondientes. Se debe entregar en tiempo y forma el lunes 07/10 antes de las 9 hs ya que se har√° la **correcci√≥n de forma sincr√≥nica y virtual**.


## Herencia

Anteriormente vimos que cuando dos objetos repiten l√≥gica, crear una clase puede que sea nuestra soluci√≥n. Pero, ¬øqu√© hacemos cuando dos clases repiten l√≥gica? Esto es un trabajo para‚Ä¶ ¬°la superclase! ü¶∏‚Äç‚ôÄÔ∏èü¶∏‚Äç‚ôÇÔ∏è

Al tener l√≥gica repetida entre clases podemos crear una nueva clase con esa l√≥gica, dejando en cada una de las clases iniciales s√≥lo lo particular de cada una. 

Por ejemplo:

*Los perros y los gatos al jugar pierden unidades de energ√≠a seg√∫n el tiempo que reciben por par√°metro. Al pedirles que emitan un sonido los perros hacen guau (s√≠, todo muy original) y los gatos‚Ä¶ ¬°MUUU! üò≤ (no, mentira, hacen miau pero casi se la creen üòÇ).
Pero al llegar su due√±o o due√±a a casa act√∫an distinto. Los gatos üêà act√∫an con indiferencia, es decir, no hacen nada. Los perros üêï en cambio aumentan en 100 su energ√≠a.*

Un c√≥digo posible podr√≠a ser:

```java
class Gato {
	var energia
	
	method jugar(unTiempo) {
		energia -= unTiempo
	}

	method emitirSonido() {
		return "miau"
}

method recibirDue√±o() {
}
}

class Perro {
	var energia
	
	method jugar(unTiempo) {
		energia -= unTiempo
	}

	method emitirSonido() {
		return "guau"
}

method recibirDue√±o() {
	energia += 100
}
}
```

¬øEsa l√≥gica repetida no les hizo doler los ojos? üòµ

Una soluci√≥n ser√≠a crear una clase Animal (no es una frase onda "CREA UNA CLASE ANIMAL!!! M√ÅQUINA!!! üí™") que contenga la l√≥gica repetida:

```java
class Animal {
	var energia
	const sonido
	
	method jugar(unTiempo) {
		energia -= unTiempo
	}

	method emitirSonido() {
		return sonido
}
}
```

Lo √∫nico que faltar√≠a es establecer una relaci√≥n entre esta nueva clase y las originales definiendo **herencia** de la siguiente manera: üëá

```java
class Perro inherits Animal(sonido = "guau") {
	method recibirDue√±o() {
	energia += 100
}
}

class Gato inherits Animal(sonido = "miau") {
	method recibirDue√±o() { }
}
```

Listo, ¬°problema solucionado! üôå Ahora vamos a decir que `Animal` es la **superclase** de `Perro` y `Gato` o, de otra manera, que `Perro` y `Gato` son **subclases** de `Animal`. Hay que tener en cuenta que cada clase solo puede heredar de una y solo una clase.

## Super

Siguiendo con nuestro ejemplo, imagin√©monos que aparece la clase `Gallina` üêî, cada `Gallina` emite el sonido "A River lo sigo a donde va" y cuando juegan tambi√©n pierde energia, peeero tambi√©n ponen un huevo üê£. Entonces tendr√≠amos que redefinir el m√©todo `jugar`, pero teniendo en cuenta que una parte de la l√≥gica ya est√° definida en la superclase `Animal`. Para hacer esto vamos a combinar `override` (para redefinir un m√©todo de la superclase) con `super` (para ver que hace la superclase):

```java
class Gallina inherits Animal(sonido = "A River lo sigo a donde va") {
	var huevosPuestos = 0

	override method jugar(unTiempo) {
	super(unTiempo)
	huevosPuestos ++
}
}
```

Podemos aprovechar `super` tanto para m√©todos que retornan algo (para obtener ese algo) como para m√©todos que no retornan nada (para ejecutar su comportamiento).

## Redefinici√≥n

Ac√° vemos que la `Gallina` no tiene el m√©todo `recibirDue√±o`, ¬ødeber√≠a? ü§î

Esto es una decisi√≥n de nuestro dise√±o, si creemos que todos los animales deber√≠an poder recibir due√±os (que los animales deban obligatoriamente tener due√±os pertenece a un debate que no vamos a tener, recordemos que esto es meramente un ejemplo), deber√≠a estar presente en nuestro c√≥digo. ¬øPero qu√© hace un animal cualquiera al recibir a su due√±o? ¬øHay alguna l√≥gica en com√∫n entre todos los animales? ¬øQu√© escribo en la superclase?

Claramente no conocemos un comportamiento gen√©rico para todos los animales, pero si queremos que todos los animales sepan recibir a su due√±o sin especificar una l√≥gica podemos crear un **m√©todo abstracto** escribiendo solo la **firma** de la siguiente manera:

```java
class Animal {
	‚Ä¶.
	method recibirDue√±o()
	‚Ä¶.
}
```

‚ùó ‚ùó Es importante diferenciar `recibirDue√±o()` de `recibirDue√±o() { }`. El segundo no es un m√©todo abstracto sino un m√©todo vac√≠o. Aquellas clases que no tiene sentido instanciarlas en nuestro dominio son llamadas **clases abstractas**. Adicionalmente, si una clase tiene un m√©todo abstracto, esta no puede ser instanciada. En nuestro ejemplo podemos tener gallinas, gatos y perros pero no animales a secas.

Luego de hacer esto es importante redefinir el m√©todo en cada subclase con la palabra `override`:

```java
class Perro inherits Animal(sonido = "guau") {
	override method recibirDue√±o() {
	energia += 100
}
}

class Gato inherits Animal(sonido = "miau") {
	override method recibirDue√±o() { }
}

class Gallina inherits Animal(sonido = "A River lo sigo a donde va") {
var huevosPuestos = 0

	override method jugar(unTiempo) {
	super(unTiempo)
	huevosPuestos ++
}

	override method recibirDue√±o() { 
	huevosPuestos = 0 /* el due√±o llega y le roba los huevos*/
}
}
```

## Method lookup

Hasta ahora vimos que cuando le enviamos un mensaje a un **well known object**, este busca el m√©todo en la definici√≥n de ese objeto üîé. Si se lo enviamos a una **instancia**, lo busca en la clase a la que pertenece. Esto sigue siendo correcto, pero tambi√©n aprendimos que si el m√©todo no est√° definido en la clase de la cual el objeto es instancia buscar√° en la superclase, y en caso que no est√© seguir√° buscando "para arriba" en la jerarqu√≠a de clases üïµÔ∏è‚Äç‚ôÄÔ∏èüïµÔ∏è‚Äç‚ôÇÔ∏è. En caso que la superclase m√°s super de todas, es decir, la clase `Object` no defina ese m√©todo, obtendremos el famoso error `wollok.lang.MessageNotUnderstoodException`.

## Interfaces

Una interfaz es un contrato que cumplen dos o m√°s clases u objetos la cual obliga a que estos cumplan con la implementaci√≥n de un conjunto de m√©todos.

Cuando dos objetos o clases tienen una interfaz en com√∫n, es posible para un tercero utilizar sus instancias de forma polim√≥rfica, aunque recordemos que el polimorfismo solo est√° si efectivamente un tercero interact√∫a indistintamente con ellos.

Las interfaces nos sirven para explicitar conjunto de mensajes que deben entender quienes las implementen. En algunos lenguajes, las interfaces se escriben en c√≥digo y existen validaciones de compilaci√≥n para asegurar su cumplimiento. En Wollok no se escriben en el c√≥digo, pero en la materia siempre vamos a explicitar las interfaces en el diagrama de clases.

¬øCu√°l es la diferencia entre una interfaz y una clase abstracta? Si bien son similares, la clase abstracta tiene como objetivo reutilizar comportamiento, ya que siempre la tenemos en c√≥digo y la idea es que otras clases puedan heredar de la misma implementando sus m√©todos. En cambio, el concepto de la interfaz es demostrar partes comunes entre clases u objetos para que puedan ser utilizados polim√≥rficamente.

## Diagrama de clases

Como vimos la clase pasada, el diagrama de clases es una herramienta que nos permite modelar nuestra soluci√≥n a partir de un esquema. En el mismo encontraremos las clases, objetos e interfaces, sus atributos, sus m√©todos y c√≥mo se relacionan estos componentes. Es una manera de representar nuestras soluciones m√°s all√° del c√≥digo.

En esta clase repasamos c√≥mo representar clases abstractas, interfaces y las flechas de "hereda de", ‚Äúusa‚Äù, "conoce" e ‚Äúimplementa‚Äù.

## Mini machete

Todo el comportamiento:
- En com√∫n, va en una **superclase**.
- En espec√≠fico, va en la **clase**.
- En com√∫n con la superclase pero que hace _alguito m√°s_, va **redefinido con super en la clase**.
- Que no me dice qu√© hacer la superclase, lo defino como **m√©todo abstracto** en la superclase y luego lo **redefino en clase**.

Si tengo clases que: 
- Comparten solo mensajes en com√∫n, necesito una **interfaz**.
- Adem√°s de los mensajes, tambi√©n comparten comportamiento, necesito una **superclase**.

Si tengo una clase que:
- Provee comportamiento y mensajes sin comportamiento, es una **clase abstracta**.
- Es todo comportamiento y no la quiero instanciar, es una **clase abstracta**.
- Es todo comportamiento y la quiero instanciar, es una **clase concreta**.

Se puede heredar tanto de clases abstractas como de clases concretas.

## Links √∫tiles 

- [Video de la clase](https://drive.google.com/file/d/1AQmfdgtekWMfe43T_vyqoSgqTVMf1GMy/view?usp=sharing)
- [C√≥digo de la clase](https://github.com/pdep-lunes/pdep-clases-2024/blob/main/Objetos/Clase05/animales_repaso.wlk) 
- [Diagrama de clases de la clase](https://github.com/pdep-lunes/pdep-clases-2024/blob/main/Objetos/Clase05/out/animales/animales.png) 
- [C√≥digo del diagrama de clases](https://github.com/pdep-lunes/pdep-clases-2024/blob/main/Objetos/Clase05/animales.plantuml) 
