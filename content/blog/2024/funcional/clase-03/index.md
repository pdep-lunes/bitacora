---
title: AplicaciÃ³n parcial y orden superior
date: "2024-04-15"
description: "Tercera clase de PdeP"
tags: [funcional, aplicacion-parcial, orden-superior, pattern-matching, tuplas, listas]
---

## Tarea para la clase que viene:
Resolver el [TP "Hora de lectura"](https://docs.google.com/document/d/1h0JQAyYL5ePak_9HS5ROgNv4afz6OE5hLXeajFG5l7o/edit). **Importante**: si bien en esta bitÃ¡cora estÃ¡ la resoluciÃ³n paso a paso y el cÃ³digo de la misma, sugerimos que intenten resolver por su cuenta el ejercicio antes de ver la soluciÃ³n (Â¡y preguntarnos lo que no se entienda!).

## Â¿QuÃ© vimos hoy? 

Comenzamos a resolver el [TP "Hora de lectura"](https://docs.google.com/document/d/11uYGXvG-TnNhveawDjKD1iSWKW9Qy8PVqlvtHhV58F8/edit) y para eso vimos estos nuevos temas:

- AplicaciÃ³n parcial: aplicar a una funciÃ³n con menos argumentos de los "normales", para obtener otra que espere los faltantes. Por ejemplo: 

```haskell
> max 6 9
9
-- max estÃ¡ aplicada totalmente (tiene los dos parÃ¡metros) y devuelve 9.

> max 6 
<function>
-- en esta ocasiÃ³n max estÃ¡ aplicada parcialmente (le falta un parÃ¡metro) y devuelve una funciÃ³n.

-- otro ejemplo con composiciÃ³n redefiniendo doble del siguiente
dobleDelSiguiente :: Num a => a -> a
dobleDelSiguiente unNumero = (*2).(+1) $ unNumero
```

- Tuplas: conjunto de elementos de diferentes tipos cuya longitud es fija. Por ejemplo:

```haskell
("Pepita", 38176598)
-- una dupla (tupla de dos elementos) con un string y un nÃºmero.
```


Para las duplas ya tenemos definidas `fst` y `snd` que devuelven el primer y el segundo elemento, respectivamente, de una tupla de dos elementos.

```haskell
fst ("Pepita", 38176598)
"Pepita"
```

```haskell
snd ("Pepita", 38176598)
38176598
```

TambiÃ©n existen las n-ternas, o sea, tuplas de N elementos. Por ejemplo:

```haskell
("hola", 1, 'e')
-- una terna (tupla de 3 elementos) compuesta de elementos de diferentes tipos.
```

Para hacer el equivalente al `fst` y `snd` de las duplas, existen lo que llamamos un **accessor**. 

```haskell
primerElemento :: (String, Int, String) -> String
primerElemento (primero, _, _) = primero
primerElemento ("hola", 1, 'e')
>> "hola"
-- una terna (tupla de 3 elementos) compuesta de elementos de diferentes tipos.
```

El uso del accessor es posible gracias a **pattern matching**, que es el concepto asociado al chequeo estructural de un dato respecto de una estructura esperada. Gracias a esto podemos tener un cÃ³digo mÃ¡s declarativo y simple. Sin embargo, su desventaja es que depende de los cambios de estructuras. ImaginÃ©monos que nosotros agregamos un cuarto elemento a la tupla. Esto harÃ­a que no fuera posible utilizar nuestro accessor inicial, ya que estructuralmente la tupla cambiÃ³.  

Algo mÃ¡s para decir de esta funciÃ³n es que estÃ¡ usando **variables anÃ³nimas** (los `_`). Las vamos a utilizar cuando necesitemos recibir un parÃ¡metro pero que no nos interesa conocer su valor (no nos es Ãºtil conocerlo) para la definiciÃ³n de la funciÃ³n.

Una cuestiÃ³n muy importante a tener en cuenta es que en funcional existen tres mundos: **el de los valores, el de los tipos y el de los patrones**. 

```haskell
nombreDeLaFuncion :: Mundo de los Tipos
nombreDeLaFuncion Mundo de los Patrones = Mundo de los Valores
```
     
Las variables anÃ³nimas solo viven en el mundo de los patrones. Por lo tanto, **van del lado izquierdo del igual y nunca deben ir del lado derecho ni en el tipado.** En el mundo de los tipos no puede ir porque tenemos que especificar los tipos de nuestras funciones y valores, aÃºn cuando son variables. En el de los valores tampoco tiene sentido porque es donde especificamos nuestros retornos, no podemos devolver "lo que sea".

- Listas: conjunto de elementos de un mismo tipo. Por ejemplo:

```haskell
[1, 2, 4, 5, 6, 8, 10, 100, 20000]
-- listas de nÃºmeros.

["die", "bart", "die"]
-- lista de strings.

[True, True]
-- lista de booleanos.

[("@skinnerOk", "Es una aurora boreal"), ("@archuN", "puedo verla??"), ("@skinnerOk", "no")]
-- listas de tuplas (representan un tweet).

[]
-- lista vacÃ­a.
```
Vimos que hay varias funciones que podemos usar con las listas:

```haskell
> length ["hola", "Â¿cÃ³mo", "estÃ¡s?"]
3
> length [6,7,8,9,10,11,12]
7
-- length: devuelve la cantidad de elementos de la lista.

> head [1,2,3,4]
1
-- head: devuelve el primer elemento de una lista

> tail [1,2,3,4]
[2,3,4]
-- tail: devuelve una nueva lista pero sin el primer elemento de la original



> elem 1 [1,2,3,4]
True

> elem 5 [1,2,3,4]
False
-- elem: devuelve si un elemento forma parte o no de una lista

> sum [1,2,3,4]
10
-- sum: devuelve la suma de todos los elementos de una lista. Â¡SÃ³lo funciona con lista de nÃºmeros!

> any even [1,2,3,4]
True
-- any: devuelve si alguno de los elementos de la lista cumplen la condiciÃ³n pasada por parÃ¡metro

> all odd [1,2,3,5,7]
False
-- all: devuelve si todos los elementos de la lista cumplen la condiciÃ³n pasada por parÃ¡metro

> filter (>4) [1,2,7,1,9]
[7,9]
-- filter: dada una condiciÃ³n (funciÃ³n que devuelve un booleano) y una lista, devuelve otra lista que contenga los elementos que cumplan la condiciÃ³n. Algo interesante de este ejemplo es que estamos usando aplicaciÃ³n parcial en (>4) ya que a la funciÃ³n (>) le estamos pasando un sÃ³lo parÃ¡metro (el 4) y estÃ¡ esperando que le llegue el que le falta (que estÃ¡ en la lista).

> map length ["hola", "murcielago"] 
[4, 10]

> map (+1) [100, 41, 26]
[101, 42, 27]
-- map: dada una funciÃ³n y una lista, devuelve otra lista que contenga a los elementos como resultado de aplicarles la funciÃ³n pasada por parametro.
```

Las funciones `map` y `filter` (y otras mÃ¡s que iremos viendo o ya vimos, como la composiciÃ³n (`.`) o la aplicaciÃ³n (`$`)) son llamadas de **orden superior** porque reciben por parÃ¡metro otra funciÃ³n ðŸ¤¯. Â¡El orden superior es buenÃ­simo porque nos permite crear funciones que reciban comportamiento (otras funciones) como argumento! De esa forma podemos pensar de forma mucho mÃ¡s declarativa: 

- Puedo aislar y reutilizar comportamiento comÃºn.
- Puedo partir mi problema, separando responsabilidades, entre el cÃ³digo que tiene orden superior, y el comportamiento parametrizado.
- Puedo tener un cÃ³digo con partes incompletas, esperando rellenarlos pasando comportamiento por parÃ¡metro, y no sÃ³lo datos.

Â¡Ahora sÃ­! Resolvamos el ejercicio. Peroâ€¦ Â¿Por dÃ³nde empezamos? ðŸ˜… Si vamos a trabajar con libros, empecemos por ahÃ­. Â¡A modelarlos! ðŸŽ¨

âš ï¸ Disclaimer: Es muy importante leer todo el enunciado antes de ponerse a codear. En este caso, vamos a ir a nuestro ritmo sÃ³lo por fines pedagÃ³gicos. âš ï¸

Tenemos que crear cada libro, para eso, vamos a crearâ€¦ Â¿variables? Â¡No! ðŸ˜  En funcional **no existen las variables** porque las cosas no varÃ­an. RecordÃ¡: **en Haskell no hay estado**. Es por eso que vamos a crear **etiquetas** ðŸ·ï¸ representando a cada libro. Â¿Y cÃ³mo los vamos a representar? ðŸ’­ Bueno, sabemos que cada libro tiene un tÃ­tulo, un autor y una cantidad de pÃ¡ginas, entonces podrÃ­amos crear a *"El visitante"* y a *"Shingeki no Kyojin capÃ­tulo 1"* de esta forma:

```haskell
elVisitante :: (String, String, Int)
elVisitante = ("el visitante", "Stephen King", 592)

shingekiNoKyojin1 :: (String, String, Int)
shingekiNoKyojin1 = ("shingeki no kyojin 1 ", "Hajime Isayama", 40)
```

Y asÃ­ seguimos con los demÃ¡s tÃ­tulos. Peeeero, antes de avanzar, Â¿no hay algo que te llame la atenciÃ³n? ðŸ¤” PongÃ¡monos a filosofar: Â¿quÃ© es `elVisitante`? Â¿quÃ© es `shingekiNoKyojin1`? Â¡Son libros ðŸ“š! Entonces, Â¿no estarÃ­a bueno poder llamarlos como corresponde?

```haskell
elVisitante :: Libro
elVisitante = ("el visitante", "Stephen King", 592)

shingekiNoKyojin1 = Libro
shingekiNoKyojin1 = ("shingeki no kyojin 1 ", "Hajime Isayama", 40)
```

Por suerte, esto no va a quedar en un deseo. Lo vamos a poder hacer realidad con el **type alias** (en criollo: un apodo o un alias). Gracias a esta herramienta vamos a lograr que nuestro cÃ³digo sea mÃ¡s expresivo ðŸ˜Œ. Entonces, para hacer esto posible deberemos agregar a nuestra soluciÃ³n un type alias:

```haskell
type Libro = (String, String, Int)

elVisitante :: Libro
elVisitante = ("el visitante", "Stephen King", 592)

shingekiNoKyojin1 = Libro
shingekiNoKyojin1 = ("shingeki no kyojin 1 ", "Hajime Isayama", 40)
```

Â¡Y podemos seguir mejorando la expresividad de nuestro cÃ³digo! Si sabemos que nuestro libro es una terna compuesta por el tÃ­tulo, autor y la cantidad de pÃ¡ginasâ€¦ MirÃ¡ ðŸ‘€:

```haskell
type Titulo = String
type Autor = String
type CantidadDePaginas = Int
type Libro = (Titulo, Autor, CantidadDePaginas)

elVisitante :: Libro
elVisitante = ("el visitante", "Stephen King", 592)

shingekiNoKyojin1 = Libro
shingekiNoKyojin1 = ("shingeki no kyojin 1 ", "Hajime Isayama", 40)
```

Y una vez que hayamos modelado todos los libros, Â¡es hora de armar la biblioteca! Para eso vamos a usar vari... Â¡Noooo! ðŸ˜¡ Crearemos una etiqueta. Â¿Y cÃ³mo va a ser la biblioteca? Bueno, una lista con los libros que modelamos. Â¿Y su tipo? Â¿Una lista de `(String, String, Int)`? Â¿O una lista de `Libro`s? ðŸ’­ Si bien dijimos que `(String, String, Int)` y `Libro` eran lo mismo, para darle un sentido semÃ¡ntico a nuestra soluciÃ³n, vamos a elegir como tipo de la biblioteca a lista de `Libro`s:

```haskell
biblioteca :: [Libro]
biblioteca = [elVisitante, shingekiNoKyojin1, shingekiNoKyojin3, shingekiNoKyojin27, fundacion, sandman5, sandman10, sandman12, eragon, eldest, brisignr, legado]
```

Sabemos lo que estÃ¡s pensandoâ€¦ tambiÃ©n nos gustarÃ­a tomar un helado ðŸ¦. Â¿Eh? Â¿Eso no era lo que pensabas? ðŸ˜³ No bueno, sÃ­, tambiÃ©n creemos que serÃ­a una buena idea crear un type alias para la biblioteca ðŸ˜…:   

```haskell
type Biblioteca = [Libro]

biblioteca :: Biblioteca
biblioteca = [elVisitante, shingekiNoKyojin1, shingekiNoKyojin3, shingekiNoKyojin27, fundacion, sandman5, sandman10, sandman12, eragon, eldest, brisignr, legado]
```

Hagamos un recreÃ­to de tanto cÃ³digo ðŸ¥´. Te vamos a contar un secreto ðŸ¤«: Â¿viste el `String`? Bueno, Â¡tambiÃ©n es un apodo! Â¿Te imaginÃ¡s cuÃ¡l es su verdadero nombre? ðŸ™ˆ Es `type String = [Char]`. AsÃ­ es, un `String` no es nada mÃ¡s ni nada menos que una lista de caracteres, una lista de `Char`. Â¿Eso significa que a partir de ahora vamos a llamarle `[Char]`? Â¡Nooo! El `String` va a seguir llamÃ¡ndose `String`, por algo alguien decidiÃ³ crear ese type alias. Â¿No nos crees? MirÃ¡ este ejemplo:

```haskell
> "Â¡Hola!" == ['Â¡', 'H', 'o', 'l', 'a', '!']
True
```

El string `Â¡Hola!` es lo mismo que la lista de caracteres `['Â¡', 'H', 'o', 'l', 'a', '!']`. Porque como dijimos, `String` es un type alias de `[Char]`. Â¡Sigamos con el TP! ðŸŒˆ

Ya modelamos los libros y la biblioteca. Es hora de definir las funciones que nos piden ðŸ‘©â€ðŸ’»ðŸ‘¨â€ðŸ’». La primera es `promedioDePaginas`. Â¿Por dÃ³nde la encaramos ðŸ˜µ? **Nuestro consejo es empezar por el tipo de la funciÃ³n**. De esa forma, vamos a tener en mente quÃ© parÃ¡metros toma y quÃ© devuelve para luego definirla. ðŸ˜Š
Â¿CuÃ¡ntos parÃ¡metros toma `promedioDePaginas`? Uno solo, la biblioteca. Entonces, vamos a empezar poniendo una sola flechita (**recordÃ¡ que el tipo de una funciÃ³n tiene la misma cantidad de flechitas que de parÃ¡metros**):

```haskell
promedioDePaginas :: ...  -> ...   
```

A veces, es muy claro quÃ© devuelve una funciÃ³n, por lo que podemos empezar completando ese espacio del tipo de la misma. En este caso, como vamos a hacer un promedio, vamos a obtener como resultado un nÃºmero entero:

```haskell
promedioDePaginas :: ...  -> Int
```

Â¿Y de quÃ© tipo es el parÃ¡metro que toma? Dijimos que era una biblioteca y el tipo de la biblioteca es `Biblioteca`:

```haskell
promedioDePaginas :: Biblioteca -> Int
```

Â¡Wooohooo ðŸŽ‰! Tenemos el tipo de nuestra funciÃ³n. Ahora, definÃ¡mosla:

```haskell
promedioDePaginas :: Biblioteca -> Int
promedioDePaginas unaBiblioteca = div (cantidadDePaginasTotales unaBiblioteca) (length unaBiblioteca)


cantidadDePaginasTotales :: Biblioteca -> Int
cantidadDePaginasTotales unaBiblioteca = sum . map cantidadDePaginas $ unaBiblioteca
 
cantidadDePaginas :: Libro -> Int
cantidadDePaginas (_, _, unasPaginas) = unasPaginas
```

Algo muy importante que hicimos en esta soluciÃ³n fue **delegar**. Es decir, dividimos nuestro gran problema en partecitas mÃ¡s pequeÃ±as para poder resolverlo mÃ¡s fÃ¡cilmente. De esta forma obtenemos un cÃ³digo mÃ¡s **declarativo**. Peeero, tampoco debemos irnos al extremo de sobredelegar: haber creado la funciÃ³n `cantidadDeLibrosDeLaBiblioteca` (que recibe una biblioteca y nos devuelve su longitud) es lo mismo que hacer directamente `length biblioteca`. La razÃ³n por la que no estÃ¡ bueno sobredelegar es que no estamos creando funciones que hagan nuevas cosas, sino que sÃ³lo estamos renombrando funciones que ya existen y conocemos.

Ahora toca el turno de definir `esLecturaObligatoria`. De vuelta vamos a descomponerla en funciones para que sea mÃ¡s fÃ¡cil construirla. ðŸ’¡ Un consejo que solemos dar es definir la funciÃ³n de tal forma que cuando la leamos, quede igual que el enunciado. Y la Ãºnica forma de hacer esto posible es delegando:

```haskell
type Saga = [Libro]

sagaDeEragon :: Saga
sagaDeEragon = [eragon, eldest, brisignr, legado]

autor :: Libro -> Autor
autor (_, unAutor, _) = unAutor

esLecturaObligatoria :: Libro -> Bool
esLecturaObligatoria unLibro = esDeStephenKing unLibro || perteneceASagaEragon unLibro || esFundacion unLibro

esDeStephenKing :: Libro -> Bool
esDeStephenKing unLibro = ((== "Stephen King") . autor) unLibro

perteneceASagaEragon :: Libro -> Bool
perteneceASagaEragon unLibro = elem unLibro sagaDeEragon

esFundacion :: Libro -> Bool
esFundacion unLibro = unLibro == fundacion
```

Fijate que la funciÃ³n `esLecturaObligatoria` quedÃ³ igual que el enunciado; el mismo dice: *â€Es una lectura obligatoria cuando es de Stephen King o de la saga de Eragon o es el ejemplar de FundaciÃ³n de 230 pÃ¡ginas de Isaac Asimovâ€*. A esto nos referimos con delegar y que se pueda leer como una oraciÃ³n del TP. ðŸ˜

Hagamos una observaciÃ³n ðŸ”: cuando creamos la `sagaDeEragon`, le pusimos como tipo `Saga`, donde saga es `[Libro]`. Â¡Lo mismo que la biblioteca! Â¿Y por quÃ© no reutilizamos el tipo `Biblioteca` si tambiÃ©n es `[Libro]` ðŸ¤¨? Porque si bien *sintÃ¡cticamente* son lo mismo, *semÃ¡nticamente* no lo son. Es decir, si bien las dos son del tipo `[Libro]`, una biblioteca no es lo mismo que una saga (y si no nos crees, buscalas en el diccionario ðŸ˜œ). Haciendo esta diferencia ganamos expresividad.

Â¡Sigamos! Es el turno de `esFantasiosa`. Comencemos con su tipo, asÃ­ que como recibe un parÃ¡metro, ponemos una flechita:

```haskell
esFantasiosa :: ... -> ...
```

Sabemos que devuelve un booleano, por lo tanto:

```haskell
esFantasiosa :: ... -> Bool
```

Y sabemos que toma una biblioteca:

```haskell
esFantasiosa :: Biblioteca -> Bool
```

TadÃ¡aa ðŸŽ‰, tenemos el tipo de nuestra funciÃ³n. Ahora definÃ¡mosla:

```haskell
esFantasiosa :: Biblioteca -> Bool
esFantasiosa unaBiblioteca = any esLibroFantasioso unaBiblioteca

esLibroFantasioso :: Libro -> Bool
esLibroFantasioso unLibro = esDeChristopherPaolini unLibro || esDeNeilGaiman unLibro

esDeChristopherPaolini :: Libro -> Bool
esDeChristopherPaolini unLibro = ((== "Christopher Paolini") . autor) unLibro

esDeNeilGaiman :: Libro -> Bool
esDeNeilGaiman unLibro = ((== "Neil Gaiman") . autor) unLibro
```

Mmmm, un momento âœ‹. Algo estÃ¡ oliendo mal ðŸ¤¢â€¦ Â¡a repeticiÃ³n de lÃ³gica! ðŸ¤® MirÃ¡ estas tres funciones:


```haskell
esDeStephenKing :: Libro -> Bool
esDeStephenKing unLibro = ((== "Stephen King") . autor) unLibro

esDeChristopherPaolini :: Libro -> Bool
esDeChristopherPaolini unLibro = ((== "Christopher Paolini") . autor) unLibro

esDeNeilGaiman :: Libro -> Bool
esDeNeilGaiman unLibro = ((== "Neil Gaiman") . autor) unLibro
```

Son prÃ¡cticamente iguales ðŸ˜±. En todas se **repite la lÃ³gica** de obtener el autor de un libro para fijarnos si es un autor en especial ðŸ˜µ. Para solucionar esto, vamos a crear una funciÃ³n que tenga sÃ³lo la lÃ³gica repetida, parametrizando lo Ãºnico que cambia (que en este caso son los nombres de los autores):

```haskell
esDe :: Autor -> Libro -> Bool
esDe unAutor unLibro = ((== unAutor) . autor) unLibro
```

Nuestra soluciÃ³n ahora quedarÃ­a asÃ­:

```haskell
esFantasiosa :: Biblioteca -> Bool
esFantasiosa unaBiblioteca = any esLibroFantasioso unaBiblioteca

esLibroFantasioso :: Libro -> Bool
esLibroFantasioso unLibro = esDe "Christopher Paolini" unLibro || esDe "Neil Gaiman" unLibro
```

Sigamos con `nombreDeLaBiblioteca`:


```haskell
titulo :: Libro -> String
titulo (unTitulo, _, _) = unTitulo

nombreDeLaBiblioteca :: Biblioteca -> String
nombreDeLaBiblioteca unaBiblioteca = sinVocales . concatenatoriaDeTitulos $ unaBiblioteca

sinVocales :: String -> String
sinVocales unString = filter (not . esVocal) unString

esVocal :: Char -> Bool
esVocal unCaracter = elem unCaracter "aeiouAEIOUÃÃ‰ÃÃ“Ãš"

concatenatoriaDeTitulos :: Biblioteca -> String
concatenatoriaDeTitulos unaBiblioteca = concatMap titulo unaBiblioteca
```

RecordÃ¡ que como un `String` es una `[Char]` es lo mismo "aeiouAEIOUÃÃ‰ÃÃ“Ãš" que ['a', 'e', 'i', 'o', 'u', 'A', 'E', 'I', 'O' , 'U', 'Ã', 'Ã‰', 'Ã', 'Ã“' , 'Ãš'] y es una forma mucho mÃ¡s fÃ¡cil de escribirlo ðŸ˜…. Si te quedÃ³ la duda de por quÃ© repetimos las vocales en minÃºscula, mayÃºscula y con tildes, es para que matchee de las dos formas. ðŸ˜„

Â¡Llegamos a la Ãºltima funciÃ³n! La que nos dice si una biblioteca es ligera:


```haskell
esBibliotecaLigera :: Biblioteca -> Bool
esBibliotecaLigera unaBiblioteca = all esLecturaLigera unaBiblioteca

esLecturaLigera :: Libro -> Bool
esLecturaLigera unLibro = ((<= 40) . cantidadDePaginas) unLibro
```

Y de esa forma completamos el TP usando las herramientas que aprendiste hasta ahora. ðŸ‘  La clase que viene seguimos. ðŸ‘‹


## Links Ãštiles

- [Enunciado que hicimos en clase](https://docs.google.com/document/d/1h0JQAyYL5ePak_9HS5ROgNv4afz6OE5hLXeajFG5l7o/edit)
- [CÃ³digo del TP](https://gist.github.com/gonziesc/c61e699bde0bd5f593015e8b568cb7fb)
- [AplicaciÃ³n parcial](http://wiki.uqbar.org/wiki/articles/aplicacion-parcial.html)
- [Orden superior](http://wiki.uqbar.org/wiki/articles/orden-superior.html)
- [Pattern Matching](http://wiki.uqbar.org/wiki/articles/pattern-matching-en-haskell.html)
- [Video de esta clase en 2021](https://drive.google.com/file/d/1f72a6efSuhUyH5KX-xXYlEXY_M4C6Zz7/view) 

