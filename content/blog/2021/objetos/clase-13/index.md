---
title: D√©cimo tercera clase
date: '2021-08-30'
description: D√©cimo tercera clase de PdeP
tags: [objetos, introducci√≥n, atributos, accessors, mensaje, m√©todo]
---

## ¬øQu√© vimos hoy?

As√≠ como en funcional ten√≠amos funciones y en l√≥gico ten√≠amos reglas l√≥gicas, como sugiere el nombre, en esta parte de la materia vamos a estar trabajando con objetos. Empecemos creando un objeto simple:

Tenemos a Poroto, el perro de una familia üêï. Por lo pronto tenemos la siguiente informaci√≥n acerca de Poroto: tiene 4 a√±os y 100 de energ√≠a (porque todos los perros necesitan energ√≠a para hacer cosas, claro). Entonces, vamos a decir que poroto tiene como **atributos** edad y energ√≠a. 

```
object poroto {
  var energia = 100
  var edad = 5
}
```


¬øY qu√© podemos hacer con poroto? Por ahora no mucho üòÖ. Estar√≠a bueno que sepa hacer m√°s cosas, como jugar. ¬øY c√≥mo hacemos que poroto juegue? ü§î Se lo tenemos que decir (poroto, jug√°! üó£Ô∏è) y la forma que tenemos para decirle a un objeto que haga algo es **envi√°ndole un mensaje**, y esto se hace as√≠:

`poroto.jugar()`

Los mensajes siempre son enviados a un objeto y son la manera de comunicarnos con los mismos.

Si corremos el c√≥digo anterior, ¬øqu√© pasa?

`ERROR: poroto does not understand jugar() (line: 4)` üí•

Claro, le dijimos a poroto que juegue, pero nunca le ‚Äúense√±amos‚Äù c√≥mo hacerlo üë®‚Äçüè´üë©‚Äçüè´. De alguna forma le tenemos que decir qu√© es lo que queremos que haga cuando le digamos que juegue, y esto lo hacemos con lo que llamamos un **m√©todo**. Repasando: un m√©todo es lo que un objeto hace cuando le enviamos un mensaje particular. Ense√±√©mosle entonces a `poroto` a jugar. Vamos a querer que cuando `poroto` juegue, su energ√≠a baje 20 unidades. Esto en el c√≥digo se escribe as√≠: üë®‚Äçüíªüë©‚Äçüíª

```
object poroto {
  var energia = 100
  var edad = 5

  method jugar() {
    energia = energia - 20 // o energia -= 20
  }
}
```

Buen√≠simo, ahora que `poroto` sabe jugar, enviemosle el mensaje de nuevo.

Ahora queremos ver si su energ√≠a cambi√≥, por lo que deber√≠amos preguntarle a poroto cu√°l es su energ√≠a. Para esto vamos a tener que declarar un _getter_.

```
method energia() {
  return energia
}
```

Si quisi√©ramos cambiarla, har√≠amos un _setter_:

```
method energia(unaEnergia) {
  energia = unaEnergia
}
```

Este tipo de m√©todos en los que accedemos al valor de un atributo o lo modificamos se llaman _accessors_. No es necesario que todos los atributos tengan los suyos, s√≥lo aquellos que los necesitan.

Por otro lado, como todo buen perro, `poroto` tiene un ladrido. Para ello crearemos el m√©todo ladrido que retorna "Guau Guau"

```
method ladrido() {
  return "Guau Guau"
}
```

La principal diferencia entre m√©todos como `ladrido` y `jugar` es que el primero retorna un valor mientras que el segundo tiene efecto de lado, es decir, modifica el estado de nuestro objeto. Es muy importante mantener diferenciados estos dos tipos y no definir comportamiento que tenga efecto y retorne en un mismo m√©todo.

Ahora, vamos a crear otro objeto: vamos a crear al due√±o de poroto, Billy. De Billy conocemos su edad, su comida favorita, su energ√≠a y su compa√±ere.
Sabemos que Billy puede jugar con poroto, entonces podemos hacer un m√©todo que sea `jugar`, el cual haga que Billy pierda 50 de energ√≠a (se cansa m√°s r√°pido que su perro) y tambi√©n hace que poroto juegue.

```
object billy {
  var edad = 12
  var comidaFavorita = "Fideos"
  var energia = 200
  var companiere = poroto

  method jugar() {
    energia -= 50
    companiere.jugar()
  }

  method companiere() {
    return companiere;
  }
}
```

Como vemos ac√°, Billy le est√° mandando un mensaje a su compa√±ere, que en este caso es poroto. Billy le dijo a su compa√±ere jug√° y Poroto jug√≥.
Ahora agregamos a Mandy que tiene como mascota a su perra Sally que empieza con la misma energ√≠a y edad que poroto y pierde la misma energ√≠a al jugar. Al jugar, Mandy pierde 30 de energ√≠a y hace que su compa√±ere juegue dos veces.

```
object mandy {
   var edad = 15
   var comidaFavorita = "Fideos"
   var energia = 200
   var companiere = sally

  method jugar() {
    energia -= 30
    companiere.jugar()
    companiere.jugar()
  }
 
  method companiere() {
    return companiere;
  }
}

object sally {
  var energia = 100
  var edad = 5

  method jugar() {
    energia = energia - 20
  }

  method ladrido() {
    return "Guau Guau"
  }
  
  method energia() {
    return energia
  }

  method energia(unaEnergia) {
    energia = unaEnergia
  }

}
```

A pesar de que `sally` y `poroto` tengan los mismos atributos, con los mismos valores iniciales y sus m√©todos sean iguales no son objetos id√©nticos, es decir, no tienen la misma identidad. Cuando hablamos de identidad nos referimos a dos objetos que sean exactamente el mismo o dicho de otra forma a dos referencias que apunten al mismo objeto.

## Links √∫tiles:

- [C√≥digo de la clase](https://github.com/pdep-lunes/pdep-clases-2021/blob/master/objetos/clase_01/src/clase_01.wlk)
- [Video de la clase](https://drive.google.com/file/d/1gBVoGA3rzSzl9D0gpFlxcEfk4485Meu_/view?usp=sharing)
