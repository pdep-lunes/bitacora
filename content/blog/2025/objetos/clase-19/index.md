---
title: Self y polimorfismo (otra vez üòù)
date: '2024-09-01'
description: D√©cimo novena clase de PdeP
tags: [objetos, self, encapsulamiento, responsabilidad, polimorfismo]
---

## Tarea para la clase que viene

- Terminar el m√©todo `llevarseComision` de `donCangrejo` del ejercicio [PdeBikini](https://docs.google.com/document/d/1EoRuoANV5XnRSKoZwtE84CqLEZrY-TqDB7sBHZ5RC2E/edit?usp=sharing).
- Realizar el ejercicio [PdepCargas](https://docs.google.com/document/d/1NyGG-c_cpgEvrHH19pM4-x1J0YevSDqVbHqD6DvvlFg/edit). ‚ö†Ô∏è Si bien tienen la soluci√≥n explicada m√°s abajo, es un buen ejercicio para practicar los conceptos vistos en clase.
- Practicar de forma individual estos [ejercicios](https://docs.google.com/document/d/11_PIuru6hAmbN1QSKpC7KDvc7IduOyBcbbVez2M8v_U/edit). Pueden subir las soluciones a un repo para que podamos revisarlas si as√≠ quisieran. 
- Practicar de forma individual con el ejercicio [¬°Qu√© fiesta!](https://docs.google.com/document/d/1jhvpQfmiLEq7tlSJfT-b0f-aajmAlW7zfU-vsNVamGI/edit). Pueden subir las soluciones a un repo para que podamos revisarlas si as√≠ quisieran.

## ¬øQu√© vimos hoy?

- Terminamos el ejercicio [¬°Qu√© fiesta!](https://docs.google.com/document/d/1jhvpQfmiLEq7tlSJfT-b0f-aajmAlW7zfU-vsNVamGI/edit). 
- Realizamos el ejercicio [PdeBikini](https://docs.google.com/document/d/1EoRuoANV5XnRSKoZwtE84CqLEZrY-TqDB7sBHZ5RC2E/edit?usp=sharing).

## Explicaci√≥n de los conceptos vistos en clase con el ejercicio [PdepCargas](https://docs.google.com/document/d/1NyGG-c_cpgEvrHH19pM4-x1J0YevSDqVbHqD6DvvlFg/edit):

### Self

Empezando a realizar el objeto `camionDeVerduras`, podr√≠amos pensar esto como primera opci√≥n:

```
object camionDeVerduras {
  var kilometraje = 700000
  var cajones = 10

  method pesoCarga() = cajones * 50

  method velocidadMaxima() = 80 - (cajones * 50) / 500
}
```
 
Esto nos presenta un problema: Tenemos una repetici√≥n de l√≥gica entre `velocidadMaxima` y `pesoCarga`. ¬øC√≥mo podr√≠amos llamar a ese mensaje?

Una opci√≥n podr√≠a ser `camionDeVerduras.pesoCarga()`, pero vemos que Wollok nos indica un _warning_:

`No debe usar el nombre del objeto dentro del mismo. Use 'self'.`

`self` es una manera con la cual el objeto se conoce a s√≠ mismo. Con `self`, puedo enviar mensajes que entienda el propio objeto, para poder abstraer l√≥gica repetida en otros m√©todos del mismo objeto. `self` es una referencia al mismo objeto.

Esto, con `self`, se puede resolver de la siguiente manera:

```
object camionDeVerduras {
  var kilometraje = 700000
  var cajones = 10

  method pesoCarga() = cajones * 50

  method velocidadMaxima() = 80 - self.pesoCarga() / 500
}
```

Hacemos el resto de los camiones, haciendo los m√©todos con las firmas necesarias (por ejemplo, `recorrerRuta` podr√≠a tener diferentes firmas y no estar en el `scannion5000`).

### Encapsulamiento

Comenzando a codificar el puesto, podr√≠amos hacer un primer acercamiento:

```
object rutatlantica {
  method pasar(unCamion) {
    pdepCargas.cobrar(7000 + 100 * unCamion.pesoCarga() / 1000)
    if (unCamion == camionDeVerduras) {
       unCamion.kilometraje(unCamion.kilometraje() + 400)
    } else if (unCamion == camionCerealero) {
       unCamion.nivelDeDeterioro(unCamion.nivelDeDeterioro() + 0.max(unCamion.velocidadMaxima().min(75) - 45))
    }
  }
}
```

Hay un poco de ruido en esta soluci√≥n: ¬øEs necesario que comparemos cami√≥n por cami√≥n para saber qu√© l√≥gica implementar? ¬øEst√° bien que esta l√≥gica la implemente el puesto? ¬øEst√° bien que el puesto modifique el estado de los camiones?

La respuesta, seg√∫n el concepto de encapsulamiento, es **no**.

El **encapsulamiento** es la buena pr√°ctica de minimizar la exposici√≥n del estado de nuestros objetos. Es decir, un objeto utiliza la _interfaz_ (mensajes que entiende) de otro para interactuar con √©l. Cada objeto es responsable de su propio estado, y no el de otros.

### Responsabilidad

La **responsabilidad**, a nivel objetos, son las cosas que el objeto es responsable de hacer. Por ejemplo, el puesto `rutatlantica` en este caso NO es responsable de cambiar el kilometraje de los camiones, pero S√ç es responsable de cobrarle a `pdepCargas` una cantidad que debe calcular.
La responsabilidad, en la programaci√≥n con objetos, est√° relacionada con qu√© objeto deber√≠a resolver las determinadas partes de nuestro problema. Si un objeto no es responsable de hacer algo lo debe delegar en el correspondiente.

Podr√≠amos hacer un segundo acercamiento:

```
object rutatlantica {
  method pasar(unCamion) {
    pdepCargas.cobrar(7000 + 100 * unCamion.pesoCarga() / 1000)
    unCamion.recorrerRuta(400, unCamion.velocidadMaxima().min(75))
  }
}
```

Ahora vemos que le vamos a querer decir a un cami√≥n que recorra una ruta. ¬øEsto funciona para todos los camiones que hicimos? ¬øQu√© pasa con la firma del m√©todo en los diferentes camiones? ¬øY si no definimos el m√©todo?

### Polimorfismo

¬øQueremos que el m√©todo funcione para todos los camiones? S√≠, ya que a todo cami√≥n que pase por el puesto (¬°sin saber cu√°l va a ser!) se le debe mandar un mensaje para que recorra 400 kms y a 75 km/h como m√°ximo.

De esto surge el **polimorfismo**: Queremos que `recorrerRuta(kilometraje, velocidad)` lo entiendan todos los camiones, para que `rutatlantica` los pueda usar indistintamente. Es decir, el **polimorfismo** en objetos es la capacidad de que un objeto pueda utilizar indistintamente a otros objetos, siendo que potencialmente estos otros sean distintos. Con esto en mente, deber√≠amos cambiar el m√©todo `recorrerRuta` en todos los camiones:

```
object camionDeVerduras {
	method recorrerRuta(extension, velocidad){
		kilometraje = kilometraje + extension
	}
}
object scanion5000 {
	method recorrerRuta(extension, velocidad){
		// no hace nada
	}
}

object camionCerealero {

	method recorrerRuta(extension, velocidad){
		nivelDeterioro += 0.max(velocidad - 45)
	}
}
```

Veamos bien qu√© es lo que est√° haciendo el m√©todo pasar:

- Se encarga de realizar el cobro a `pdepCargas`.
- Realiza el c√°lculo para saber cu√°nto hay que cobrarle.
- Le dice a un cami√≥n que recorra una ruta.
- Calcula la velocidad a la cual el cami√≥n debe de recorrer la ruta.


Dentro de estas cuatro cosas que est√° haciendo el m√©todo `pasar()`, ¬øEs _responsable_ el m√©todo de hacerlo todo? ¬øA qu√© nos referimos por responsable?

Nos referimos por responsabilidad, en m√©todos, a eso que el m√©todo debe de hacer. Es decir, por ejemplo, que nuestro m√©todo `pasar()` tiene responsabilidad ‚Äúde m√°s‚Äù: Cuando pasa un cami√≥n s√≥lo le queremos cobrar a `pdepCargas` una cantidad y decirle al cami√≥n que recorra la ruta. El resto de la l√≥gica podr√≠amos abstraerla, para poder tener un c√≥digo m√°s legible y con m√©todos con sus respectivas ‚Äúresponsabilidades‚Äù.


Esto se ver√≠a de la siguiente manera:

```
object rutatlantica {
	method pasar(unCamion) {
	  pdepCargas.cobrar(self.costo(unCamion))
	  unCamion.recorrerRuta(400, self.velocidadQuePasa(unCamion))
	}

	method velocidadQuePasa(unCamion) {
	  return unCamion.velocidadMaxima().min(75)
	}

	method costo(unCamion) {
	  return 7000 + 100 * unCamion.pesoCarga() / 1000
	}
}
```

## Links √∫tiles:

- [C√≥digo de Pdepcargas](https://github.com/pdep-lunes/pdep-clases-2024/tree/main/Objetos/Clase02/pdep-cargas/pdep-cargas)
- [C√≥digo de la clase de hoy](https://github.com/pdep-lunes/pdep-clases-2025/blob/master/Objetos/Clase19/pdepbikini/pdepbikini.wlk)
- [Pdep cargas](https://docs.google.com/document/d/1NyGG-c_cpgEvrHH19pM4-x1J0YevSDqVbHqD6DvvlFg/edit)
