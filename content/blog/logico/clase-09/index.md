---
title: Novena clase
date: '2020-07-06'
description: Novena clase de PdeP'
tags: [forall, functores, is, pattern-matching, polimorfismo]
---

## Tarea: 
- Entregar la [primera parte del TP grupal](https://docs.google.com/document/d/1bblUbyuVNoGQKDRVq0usSkHEIts7WXNrkTMQlnkJC9w/edit) para el lunes que viene (13/7).

## ¬øQu√© vimos hoy?
- Forall
- Functores
- is
- Pattern Matching
- Polimorfismo

## Forall

Hasta ahora, estuvimos trabajando con **cuantificadores existenciales**, es decir, todas nuestras consultas eran del tipo:

```
‚àÉx / p(x) => q(x)
```

Pero, sabemos que hay otro cuantificador, el *universal*. ¬øY c√≥mo podemos trabajar con este cuantificador? üëÄ

Vamos a utilizar el predicado de orden superior `forall/2`:

```prolog
forall(antecedente, consecuente).
```

Para que el `forall` responda verdadero, tiene que ocurrir que **para todo antecedente que ocurra, su consecuente ocurre**.

Por ejemplo, al definir el predicado `soloEscribioComics/1` nos responde si todas las obras que alguien escribi√≥ son c√≥mics.

```prolog
soloEscribioComics(Artista) :-
    escribio(Artista, _),
    forall(escribio(Artista, Obra), esComic(Obra)).
```

Cuidado, ya que forall no es conmutativo, si por ejemplo hici√©semos:

```prolog
forall(esComic(Obra), escribio(Artista, Obra)).
```

Esto, a diferencia de lo que hicimos en `soloEscribioComics`, significa que para todo c√≥mic que existe lo escribi√≥ ese Artista.

Al igual que `not`, el `forall` es un  predicado de orden superior ya que recibe predicados por par√°metro. ¬øY por qu√© ligamos al `Artista` antes de entrar al `forall`? Porque, siguiendo con las similitudes con el `not`, `forall` no liga variables. Es por este motivo que tenemos que _"generar"_ al `Artista` antes de entrar al `forall` pero teniendo la precauci√≥n de no ligar la `Obra`, ya que queremos **todas** las obras de **un** artista.

## Functores

Hasta ahora, en l√≥gico siempre hicimos consultas en las cuales nuestros individuos eran simples.
Pero adem√°s de los individuos simples, tambi√©n existen los **individuos complejos**. üßê
Los *functores* son individuos complejos y tienen:
- Un nombre, o etiqueta, que los identifica.
- Una aridad.

En nuestra base de conocimientos ten√≠amos libros, ahora conocemos los tipos de libros, que pueden ser:
- `novela(Genero, CantidadDeCapitulos)`
- `libroDeCuentos(CantidadDeCuentos)`
- `cientifico(Disciplina)` 
- `bestSeller(Precio, CantidadDePaginas)`

Adem√°s contamos con un predicado `esDeTipo/2` que relaciona a cada libro con su tipo.

Ahora queremos saber si un libro est√° bueno, esto se cumple cuando:
- Es una novela policial y tiene menos de 12 cap√≠tulos.
- Es una novela de terror.
- Los libros con m√°s de 10 cuentos siempre son buenos.
- Es un libro cient√≠fico de fisicaCuantica.
- Es un best seller y el precio por p√°gina es menor a $50.

```prolog
estaBueno(Libro):-
    esDeTipo(Libro, novela(policial, Capitulos)),
    Capitulos < 12.
estaBueno(Libro):-
    esDeTipo(Libro, novela(terror, _)).
estaBueno(Libro):-
    esDeTipo(Libro, libroDeCuentos(CantidadDeCuentos)),
    CantidadDeCuentos > 10.
estaBueno(Libro):-
    esDeTipo(Libro, cientifico(fisicaCuantica)).
estaBueno(Libro):-
    esDeTipo(Libro, bestSeller(Precio, Paginas)),
    Precio / Paginas < 50.
```
> Si bien los functores se escriben como un predicado, **NO** son un predicado. Como ven, los estamos usando como par√°metro en nuestras consultas.

Ahora, mirando üî≠ un poco a nuestro c√≥digo anterior, podemos ver l√≥gica repetida y eso no nos gusta mucho. üëé

Veamos c√≥mo podr√≠amos cambiar esto...


```prolog
estaBueno(Libro):-
    esDeTipo(Libro, Tipo),
    esTipoCopado(Tipo).

esTipoCopado(novela(policial, Capitulos)):-
    Capitulos < 12.
esTipoCopado(novela(terror, _)).
esTipoCopado(libroDeCuentos(CantidadDeCuentos)):-
    CantidadDeCuentos > 10.
esTipoCopado(cientifico(fisicaCuantica)).
esTipoCopado(bestSeller(Precio, Paginas)):-
    Precio / Paginas < 50.
```

Tenemos un par de cosas interesantes aqu√≠. ü§î

Primero, si prestamos atenci√≥n a c√≥mo definimos el predicado `esTipoCopado`, podemos ver que estamos **deconstruyendo** (o abriendo) a los functores que representan el tipo de los libros.  
Por ejemplo, en esta cla√∫sula, el tipo va a unificar s√≥lo con aquellas novelas que sean policiales.
```prolog
esTipoCopado(novela(policial, Capitulos)):-
    Capitulos < 12.
% adem√°s la variable Capitulos va a unificar
% con la cantidad de cap√≠tulos de esa novela
```
Adem√°s, cada functor va a "saber" con qu√© cla√∫sula del predicado quedarse.  
Todo esto gracias a que tenemos *Pattern Matching*. üéâ

En segundo lugar, podemos ver como al predicado `tipoCopado` no le importa para nada que *forma* puede llegar a tener el functor del tipo de libro.  
Al hecho de tratar variables indistintamente de su forma lo llamamos **polimorfismo** üíó y va a ser un concepto muy importante y √∫til desde ahora hasta siempre. üò±

## is

`is` es un predicado que relaciona a una cuenta *(a la derecha)* con su resultado *(a la izquierda)*. Es inversible para el resultado: liga la variable del `Resultado` al resultado de la expresi√≥n matem√°tica de la derecha.
```prolog
Resultado is ExpresionMatematica
```
Entonces:
- ¬øCu√°ndo usamos `is`?  
Bueno, **solamente** cuando necesitamos realizar cuentas que tienen sentido.
- ¬øY en en d√≥nde m√°s?  
En ning√∫n otro caso. Usar `is` s√≥lo para ligar variables est√° **MAL** y nadie quisiera reprobar su parcial de l√≥gico por eso. üëÆüèª‚Äç

En la clase lo utilizamos cuando hicimos `cantidadDePaginas`:

```prolog
cantidadDePaginas(Libro, CantidadDePaginas) :- 
	esDeTipo(Libro, Tipo),
	paginasPorTipo(Tipo, CantidadDePaginas).

paginasPorTipo(novela(_, CantidadDeCapitulos), CantidadDePaginas):-
	CantidadDePaginas is CantidadDeCapitulos * 20.

paginasPorTipo(libroDeCuentos(CantidadDeCuentos), CantidadDePaginas):-
	CantidadDePaginas is CantidadDeCuentos * 5.

paginasPorTipo(cientifico(_), 1000).

paginasPorTipo(bestSeller(_, CantidadDePaginas), CantidadDePaginas).
```

Pero fijate que solo lo utilizamos en las novelas y en los libros de cuentos. Para los best seller y los libros cient√≠ficos usamos Pattern Matching ¬°porque no hab√≠a cuentas!

## Links √∫tiles:

- [Video de la clase de hoy](https://drive.google.com/file/d/1FLtumH4JNb34qIfYd0aPZyA8QHN5EnEM/view?usp=sharing)
- [Generaci√≥n](http://wiki.uqbar.org/wiki/articles/paradigma-logico---generacion.html)
- [Forall](http://wiki.uqbar.org/wiki/articles/paradigma-logico---existe-vs-para-todo.html)
- [Functores](http://wiki.uqbar.org/wiki/articles/paradigma-logico---functores.html)
- [Polimorfismo](http://wiki.uqbar.org/wiki/articles/polimorfismo-en-el-paradigma-logico.html)
- [Aritm√©tica/is](http://wiki.uqbar.org/wiki/articles/aritmetica-en-prolog.html)
